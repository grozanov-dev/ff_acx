#!/usr/bin/perl -w -- 
#
# generated by wxGlade 0.9.4 on Thu Mar 16 03:56:04 2023
#
# To get wxPerl visit http://www.wxperl.it
#

use Wx qw[:allclasses];
use strict;

# begin wxGlade: dependencies
# end wxGlade

# begin wxGlade: extracode
# end wxGlade

package MainFrame;

use Wx qw[:everything];
use base qw(Wx::Frame);
use strict;
use File::Slurp qw/read_file/;
use JSON qw/decode_json/;
use Data::Dumper;

use constant _TABS => [
    [ 'video_panel', 'Video' ],
    [ 'audio_panel', 'Audio' ],
];

use constant _LAYOUT => {
    video_panel => [
        [ 'TextCtrl::inFile', wxID_ANY, '', wxDefaultPosition, [200, 34] ],
        [ 'Button::doLoad',   wxID_ANY, 'Load' ],
        [ 'Button::doPlay',   wxID_ANY, 'Play' ],
    ],
};


sub new {
    my( $self, $title ) = @_;

    $self = $self->SUPER::new(
        undef,
        -1,
        $title,
        wxDefaultPosition,
        Wx::Size->new(800, 600),
        wxDEFAULT_DIALOG_STYLE,
        ''
    );

    $self->readConfig('./ff_conf.json');

    $self->{main_sizer}  = Wx::BoxSizer->new(wxHORIZONTAL);

    $self->createTabs('main_sizer');
    $self->createWidgets('video_panel');

    $self->createLayout('main_sizer');

    return $self;

}

sub createLayout {
    my ( $self, $sizer ) = @_;

    $self->SetSizer( $self->{ $sizer } );

    $self->Layout;
}

sub createWidgets {
    my ($self, $tab_name) = @_;
    my $layout = _LAYOUT()->{ $tab_name };

    for my $desc ( @$layout ) {
        my ($class, $name) = split /::/, shift @$desc;
        my $wxClass = 'Wx::' . $class;

        $self->{ $name } = eval { $wxClass->new( $self->{ $tab_name }->{ page }, @$desc ) };

        $self->BtnEvent( $name ) if $class eq 'Button';
        $self->{ $tab_name }->{ sizer }->Add($self->{ $name }, 0, 0, 0);
    }
}

sub createTabs {
    my ( $self, $sizer_name ) = @_;
    my $book = Wx::Notebook->new($self, wxID_ANY);

    for my $tab ( _TABS()->@* ) {
        my ($tab_name, $tab_label) = @$tab;

        $self->{ $tab_name } = {};
        $self->{ $tab_name }->{ sizer } = Wx::BoxSizer->new(wxHORIZONTAL);
        $self->{ $tab_name }->{ page  } = Wx::Panel->new($book, wxID_ANY);
        $self->{ $tab_name }->{ page }->SetSizer($self->{ $tab_name }->{ sizer });

        $book->AddPage($self->{ $tab_name }->{ page }, $tab_label);
    }

    $self->{ $sizer_name }->Add( $book, 1, wxEXPAND, 0 );
}

sub readConfig {
    my ( $self, $path ) = @_;
    my $config = read_file( $path );

    $self->{Conf} = decode_json( $config );
}

sub doPlay {
    my ( $self ) = @_;
    my $file = $self->{ inFile }->GetValue;

    my $vf = "drawtext=text='timestamp\: %{pts \\: hms}':fontsize=72:r=60:x=(w-tw)/2:y=h-(2*lh):fontcolor=white:box=1:boxcolor=0x00000099";

    `ffplay -i "$file" -vf "$vf"`;
}

sub doLoad {
    my ( $self ) = @_;
    $self->_LoadFile('inFile', 'Load file', '*.*', './');
}

sub _LoadFile {
    my ($self, $field, $caption, $wildcard, $defaultDir) = @_;

    my $fileDialog = Wx::FileDialog->new(
        $self,
        $caption,
        $defaultDir,
        '',
        $wildcard,
        wxFD_OPEN
    );

    my $status = $fileDialog->ShowModal();

    my $dir  = $fileDialog->GetDirectory();
    my $file = $fileDialog->GetFilename();

    $self->{ $field }->Clear();
    $self->{ $field }->WriteText($dir . '/' . $file);

    $self->{ $field }->Clear() if $status == wxID_CANCEL;
}

sub BtnEvent {
    my ($self, $evt) = @_;

    Wx::Event::EVT_BUTTON($self, $self->{ $evt }->GetId, $self->can( $evt ));
}

1;

package ACXSync;

use base qw(Wx::App);
use strict;

sub OnInit {
    my( $self ) = shift;

    Wx::InitAllImageHandlers();

    my $frame = MainFrame->new( 'ACX Tools' );

    $self->SetTopWindow($frame);
    $frame->Show(1);

    return 1;
}

package main;

unless(caller){
    my $acx = ACXSync->new();
    $acx->MainLoop();
}
